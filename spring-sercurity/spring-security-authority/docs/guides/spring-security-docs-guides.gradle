apply plugin: 'org.asciidoctor.jvm.convert'
apply plugin: 'io.spring.convention.repository'

asciidoctor {
	baseDir = file('src/docs/asciidoc')
	options eruby: 'erubis'

	attributes copycss : '',
			icons : 'font',
			'source-highlighter': 'prettify',
			sectanchors : '',
			toc: '',
			'toc-placement' : 'preamble',
			idprefix: '',
			idseparator: '-',
			doctype: 'book',
			'spring-security-version' : project.version,
			'download-url' : getDownloadUrl(),
			'include-maven-repository' : getMavenRepositoryInclude(),
			revnumber : project.version
}

ext.spec = copySpec {
	into ('guides') {
		from(asciidoctor.outputDir)
		exclude 'build', 'Guardfile'
	}
}

def getDownloadUrl() {
	snapshotBuild ? "https://github.com/spring-projects/spring-security/archive/master.zip" : "https://github.com/spring-projects/spring-security/archive/${project.version}.zip"
}


def getMavenRepositoryInclude() {
	if(snapshotBuild) {
		return "_includes/maven-repository-snapshot.asc"
	} else if(releaseBuild) {
		return "_includes/maven-repository-release.asc"
	} else {
		return "_includes/maven-repository-milestone.asc"
	}
}
